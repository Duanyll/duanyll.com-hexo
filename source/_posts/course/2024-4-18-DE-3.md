---
title: 微分方程数值解作业 3
tags:
  - 微分方程
tikzjax: true
---

## Problem 1

![](https://img.duanyll.com/img/20240418173041.png)

### Question (a)

Assume (3.30)

$$
u_{i,j}=w_je^{rx_iI},
\label{eqn330}
$$

and substitute it into (3.51)

$$
\begin{aligned}
    u_{i,j+1}-u_{i,j}=\;&(1-\theta)(\lambda u_{i-1,j+1}-2\lambda u_{i,j+1}+\lambda u_{i+1,j+1}-kf_{i,j+1})\\
    &+\theta(\lambda u_{i-1,j}-2\lambda u_{i,j}+\lambda u_{i+1,j}-kf_{i,j})+k\tau_{i,j}.
\end{aligned}
\label{eqn351}
$$

With setting $f(x,t)=0$, we have

$$
\begin{aligned}
    w_{j+1}e^{rx_iI}-w_je^{rx_iI}=\;&(1-\theta)\lambda w_{j+1}(e^{r(x_i-h)I}-2e^{rx_iI}+e^{r(x_i+h)I})\\
    &+\theta\lambda w_j(e^{r(x_i-h)I}-2e^{rx_iI}+e^{r(x_i+h)I}).
\end{aligned}
$$

Collecting terms with $w_{j+1}$ and $w_j$ on each side, we have

$$
\begin{aligned}
    w_{j+1}e^{rx_iI}&\left(\lambda(\theta-1)e^{-rhI}+2\lambda(1-\theta)+1+\lambda(\theta-1)e^{rhI}\right)\\
    =w_je^{rx_iI}&\left(\lambda\theta e^{-rhI}+1-2\lambda\theta+\lambda\theta e^{rhI}\right).
\end{aligned}
$$

Use the identity $2\cos\theta=e^{I\theta}+e^{-I\theta}$, we have

$$
\begin{aligned}
    w_{j+1}e^{rx_iI}&\left(2\lambda(1-\theta)+1+2\lambda(\theta-1)\cos rh\right)\\
    =w_je^{rx_iI}&\left(1-2\lambda\theta+2\lambda\theta\cos rh\right).
\end{aligned}
$$

Then use $1-\cos\theta=2\sin^2\frac{1}{2}\theta$, we can get

$$
\begin{aligned}
    w_{j+1}e^{rx_iI}&\left(1+4\lambda(1-\theta)\sin^2\frac{rh}{2}\right)\\
    =w_je^{rx_iI}&\left(1-4\lambda\theta\sin^2\frac{rh}{2}\right).
\end{aligned}
$$

Finally, we have

$$
w_{j+1}=\frac{1-4\lambda\theta\sin^2\frac{rh}{2}}{1+4\lambda(1-\theta)\sin^2\frac{rh}{2}}w_j,
$$

which contains the desired $\kappa$.

### Question (b)

The stability condition is

$$
\left|\frac{1-4\lambda\theta\sin^2\frac{rh}{2}}{1+4\lambda(1-\theta)\sin^2\frac{rh}{2}}\right|\leq 1.
$$

Breakdown the absolute value, we have

$$
-(1+4\lambda(1-\theta)\sin^2\frac{rh}{2})\leq 1-4\lambda\theta\sin^2\frac{rh}{2}\leq 1+4\lambda(1-\theta)\sin^2\frac{rh}{2}.
$$

Since $\lambda\geq 0$, the right inequality is always true. For the left inequality, we have

$$
4\lambda(2\theta-1)\sin^2\frac{rh}{2}\leq 2,
$$

which holds irrespective of the value of $r$ under the condition

$$
\lambda(2\theta-1)\leq\frac{1}{2}.
$$

## Problem 2

![](https://img.duanyll.com/img/20240418173058.png)

![](https://img.duanyll.com/img/20240419225022.png)

### Question (a)

The stencil is shown below.

```{=latex}
\begin{tikzpicture}
\draw[gray, thick] (-2,1) -- (2,1);
\draw[gray, thick] (-2,0) -- (2,0);
\draw[gray, thick] (-2,-1) -- (2,-1);
\draw[gray, thick] (-1,-1.5) -- (-1,1.5);
\draw[gray, thick] (0,-1.5) -- (0,1.5);
\draw[gray, thick] (1,-1.5) -- (1,1.5);
\draw (-2.5,1) node {$t_{j+1}$};
\draw (-2.5,0) node {$t_j$};
\draw (-2.5,-1) node {$t_{j-1}$};
\draw (-1,-2) node {$x_{i-1}$};
\draw (0,-2) node {$x_i$};
\draw (1,-2) node {$x_{i+1}$};
\draw[red] (0,1) circle [radius=0.15];
\filldraw[red] (1,0) circle [radius=0.15];
\filldraw[red] (0,-1) circle [radius=0.15];
\filldraw[red] (-1,0) circle [radius=0.15];
\end{tikzpicture}
```

Limits on $i$ and $j$ are

$$
1\leq i\leq N, \quad 1\leq j\leq M-1.
$$

### Question (b)

FDE for the Dufort-Frankel scheme is

$$
\frac{u_{i+1,j}-u_{i,j+1}-u_{i,j-1}+u_{i-1,j}}{h^2}+\tau_{i,j}=\frac{u_{i,j+1}-u_{i,j-1}}{2k}.
$$

Extract $\tau_{i,j}$ and use Taylor expansion ($x_i,t_j$ subscript is omitted), we have

$$
\begin{aligned}
\tau&=\frac{1}{2k}(u+ku_t+\frac{k^2}{2}u_{tt}+\frac{k^3}{6}u_{ttt}+O(k^4))-\frac{1}{2k}(u-ku_t+\frac{k^2}{2}u_{tt}-\frac{k^3}{6}u_{ttt}+O(k^4))\\
&-\frac{1}{h^2}(u+hu_x+\frac{h^2}{2}u_{xx}+\frac{h^3}{6}u_{xxx}+\frac{h^4}{24}u_{xxxx}+O(h^5))\\
&-\frac{1}{h^2}(u-hu_x+\frac{h^2}{2}u_{xx}-\frac{h^3}{6}u_{xxx}+\frac{h^4}{24}u_{xxxx}+O(h^5))\\
&+\frac{1}{h^2}(u+ku_t+\frac{k^2}{2}u_{tt}+O(k^3))+\frac{1}{h^2}(u-ku_t+\frac{k^2}{2}u_{tt}+O(k^3))\\
&=u_t-u_{xx}+\frac{k^2}{6}u_{ttt}+O(k^3)-\frac{h^2}{12}u_{xxxx}+O(h^3)+\frac{k^2}{h^2}u_{tt}+O(\frac{k^3}{h^2}).
\end{aligned}
$$

Use $u_{xx}=u_t$ and drop higher order terms, we have

$$
\tau=\left(\frac{k^2}{h^2}-\frac{h^2}{12}\right)u_{tt}+\frac{k^2}{6}u_{ttt}.
$$

This is of order $O(k^2)+O(h^2)+O(\frac{k^2}{h^2})$. Due to the presence of $\frac{k^2}{h^2}$ term, the method is not consistent when $\frac{k}{h}>1$, where $\tau\to\infty$ when $h\to 0$. Therefore the method is only conditionally consistent.

### Question (c)

The method is explicit. $u_{i,j+1}$ can be directly calculated from $u_{i-1,j}$, $u_{i+1,j}$ and $u_{i,j-1}$ without solving a linear system or inverse function.

### Question (d)

Use $\lambda$ notation, the FDE can be written as

$$
2\lambda(u_{i+1,j}-u_{i,j+1}-u_{i,j-1}+u_{i-1,j})=u_{i,j+1}-u_{i,j-1}.
$$

Extract $u_{i,j+1}$, we have

$$
u_{i,j+1}=\frac{2\lambda(u_{i+1,j}+u_{i-1,j})+(1-2\lambda)u_{i,j-1}}{1+2\lambda}.
$$

Substitute $u$ with the generic solution $(\ref{eqn330})$, we have

$$
w_{j+1}e^{rx_iI}=\frac{2\lambda(w_{j}e^{r(x_i+h)I}+w_{j}e^{r(x_i-h)I})+(1-2\lambda)w_{j-1}e^{rx_iI}}{1+2\lambda}.
$$

Assume $w_j=\kappa^j$, and divide both sides by $\kappa^{j-1}e^{rx_iI}$, we have

$$
\begin{aligned}
    \kappa^2&=\frac{2\lambda\kappa(e^{rhI}+e^{-rhI})+(1-2\lambda)}{1+2\lambda}\\
    &=\frac{4\lambda\kappa\cos rh+(1-2\lambda)}{1+2\lambda},
\end{aligned}
$$

which yields the following 2nd order equation

$$
(1+2\lambda)\kappa^2-4\lambda\kappa\cos rh-(1-2\lambda)=0.
$$

The roots of this equation are

$$
\begin{aligned}
    \kappa_{\pm}&=\frac{4\lambda\cos rh\pm\sqrt{16\lambda^2\cos^2 rh+4(1+2\lambda)(1-2\lambda)}}{2(1+2\lambda)}\\
    &=\frac{2\lambda\cos rh\pm\sqrt{1-4\lambda^2\sin^2 rh}}{1+2\lambda}.
\end{aligned}
$$

For the method to be stable, we need $|\kappa_{\pm}|\leq 1$. For real roots where $1-4\lambda^2\sin rh\geq 0$, we have

$$
|\kappa_\pm|\leq\frac{2\lambda|\cos rh|+\sqrt{1-4\lambda^2\sin^2 rh}}{1+2\lambda}\leq\frac{2\lambda+1}{1+2\lambda}=1.
$$

For complex roots where $1-4\lambda^2\sin rh<0$, we have

$$
\sqrt{1-4\lambda^2\sin^2 rh}=I\sqrt{4\lambda^2\sin^2 rh-1},
$$

and

$$
\kappa_\pm\conj{\kappa_\pm}=\frac{(2\lambda\cos rh)^2+(4\lambda^2\sin^2 rh-1)}{(1+2\lambda)^2}=\frac{4\lambda^2-1}{(2\lambda+1)^2}=\frac{2\lambda-1}{2\lambda+1}\leq 1.
\label{eqn3}
$$

Note that $1-4\lambda^2\sin rh<0$ guarantees that $\lambda>\frac{1}{2}$, and the method is unconditionally stable.

### Question (e)

From $(\ref{eqn3})$, we have

$$
\lim_{\lambda\to+\infty}\kappa_\pm\conj{\kappa_\pm}=\lim_{\lambda\to+\infty}\frac{2\lambda-1}{2\lambda+1}=1.
$$

This indicates that though $w_j$ deacys exponentially, it does so slowly when $\lambda$ is large. Therefore, the method is stable but not L-stable.

## Problem 3

![](https://img.duanyll.com/img/20240418173115.png)

![](https://img.duanyll.com/img/20240420172215.png)

### Question (a)

From equation (3.22)

$$
u_{i,j+1}=\lambda u_{i-1,j}+(1-2\lambda)u_{i,j}+\lambda u_{i+1,j}-kf_{i,j}+k\tau_{i,j},
$$

we can replace terms with Taylor expansion at $(x_i,t_j)$, and obtain

$$
\begin{aligned}
    &u(x_i,t_j)+ku_t(x_i,t_j)+\frac{k^2}{2}u_{tt}(x_i,t_j)+O(k^3) \\
    =\;&\frac{k}{h^2}\left(u(x_i,t_j)-hu_x(x_i,t_j)+\frac{h^2}{2}u_{xx}(x_i,t_j)-\frac{h^3}{6}u_{xxx}(x_i,t_j)+\frac{h^4}{24}u_{xxxx}(x_i,t_j)+O(h^5)\right)\\
    +\;&\frac{k}{h^2}\left(u(x_i,t_j)+hu_x(x_i,t_j)+\frac{h^2}{2}u_{xx}(x_i,t_j)+\frac{h^3}{6}u_{xxx}(x_i,t_j)+\frac{h^4}{24}u_{xxxx}(x_i,t_j)+O(h^5)\right)\\
    +\;&(1-\frac{2k}{h^2})u(x_i,t_j)-k(u_{xx}(x_i,t_j)-u_t(x_i,t_j))+k\tau_{i,j}.\\
\end{aligned}
\label{eqn1}
$$

Cancel duplicated terms, we have

$$
\tau_{i,j}=\frac{k}{2}u_{tt}(x_i,t_j)+O(k^2)-\frac{h^2}{12}u_{xxxx}(x_i,t_j)+O(h^4).
$$

where $\tau_{i,j}$ has an order of $O(h^2)+O(k)$. Note that the fifth spatial derivative terms in $(\ref{eqn1})$ also cancel out, so the remain in $\tau$ is $O(h^4)$ instead of $O(h^3)$.

### Question (b) (c)

Taking $6k=h^2$, we have

$$
\tau_{i,j}=\frac{h^2}{12}\left(u_{tt}(x_i,t_j)-u_{xxxx}(x_i,t_j)\right)+O(h^4).
$$

When applied to the heat equation

$$
u_{xx}=u_t,
$$

we have

$$
u_{xxt}=u_{tt},
$$

assuming $u$ is smooth enough, we have

$$
u_{xxt}=u_{txx}=u_{xxxx}=u_{tt}.
$$

Therefore, we can see that the $O(h^2)+O(k)$ terms in $\tau_{i,j}$ cancel out, and the error is reduced to $O(h^4)$.

### Question (d)

Apply Taylor expansion to $(\ref{eqn351})$, we have

$$
\begin{aligned}
    &u+ku_t+\frac{k^2}{2}u_{tt}+O(k^3)-u\\
    =\;&(1-\theta)\frac{k}{h^2}\left(u+ku_t+hu_x+\frac{k^2}{2}u_{tt}+khu_{xt}+\frac{h^2}{2}u_{xx}+\frac{k^3}{6}u_{ttt}+\frac{k^2h}{2}u_{xtt}+\frac{kh^2}{2}u_{xxt}+\frac{h^3}{6}u_{xxx}+O((k+h)^4)\right)\\
    +\;&(1-\theta)\frac{k}{h^2}\left(u+ku_t-hu_x+\frac{k^2}{2}u_{tt}-khu_{xt}+\frac{h^2}{2}u_{xx}+\frac{k^3}{6}u_{ttt}-\frac{k^2h}{2}u_{xtt}+\frac{kh^2}{2}u_{xxt}-\frac{h^3}{6}u_{xxx}+O((k+h)^4)\right)\\
    -\;&2(1-\theta)\frac{k}{h^2}\left(u+ku_t+\frac{k^2}{2}u_{tt}+\frac{k^3}{6}u_{ttt}+O(k^4)\right)\\
    +\;&\theta\frac{k}{h^2}\left(u+hu_x+\frac{h^2}{2}u_{xx}-\frac{h^3}{6}u_{xxx}+O(h^4)\right)\\
    +\;&\theta\frac{k}{h^2}\left(u-hu_x+\frac{h^2}{2}u_{xx}+\frac{h^3}{6}u_{xxx}+O(h^4)\right)\\
    -\;&2\theta\frac{k}{h^2}u-k(u_{xx}-u_t)+k\tau.
\end{aligned}
\label{eqn2}
$$

$(x_i,t_j)$ omitted. Cancel out terms to get

$$
\frac{k^2}{2}u_{tt}+O(k^3)=2(1-\theta)\frac{k}{h^2}\left(\frac{kh^2}{2}u_{xxt}+O((k+h)^4)\right)+k\tau.
$$

And extract $\tau$

$$
\tau=\frac{k}{2}u_{tt}-(1-\theta)ku_{xxt}+O(k^2)+O(h^2).
$$

It is possible but tedious to show that $O((k+h)^4)$ in $(\ref{eqn2})$ can be finally reduced to $O(h^2)$ in $\tau$. $\tau$ is at least of $O(k)+O(h^2)$ order. In heat equation, we have $u_{xxt}=u_{tt}$, which indicates that when $\theta=\frac{1}{2}$, the two terms in $\tau$ cancel out, and the error is reduced to $O(k^2)+O(h^2)$.

### Question (e)

![](https://img.duanyll.com/img/20240420225215.png)

In the above sub-figure, $\lambda$ is fixed for each curve and $k$ is varing along the horizontal axis. This does not match situation in the question.

![](https://img.duanyll.com/img/20240420225724.png)

This sub-figure has fixed $h$ for each curve, and $k$ is decreasing along the horizontal axis. For a given $k$, $N=20$ curve has larger $h$ and smaller $\lambda$. It can be observed that $N=20$ curve has larger error than $N=40$ curve at the same $k$, indicating that decreasing $\lambda$ while keeping $k$ increases the error. From this perspective, there is no contradiction.

## Problem 4

![](https://img.duanyll.com/img/20240418173132.png)

![](https://img.duanyll.com/img/20240418173140.png)

### Question (a)

```mathematica
makeAMatrix[n_, \[Lambda]_] :=
  DiagonalMatrix[ConstantArray[1 - 2 \[Lambda], n]] +
   DiagonalMatrix[ConstantArray[\[Lambda], n - 1], 1] +
   DiagonalMatrix[ConstantArray[\[Lambda], n - 1], -1];
makeBMatrix[n_, \[Lambda]_] :=
  DiagonalMatrix[ConstantArray[1 + 2 \[Lambda], n]] +
   DiagonalMatrix[ConstantArray[-\[Lambda], n - 1], 1] +
   DiagonalMatrix[ConstantArray[-\[Lambda], n - 1], -1];
explicitIt[u1_, f1_, f2_, k_, a_, b_] := a . u1 - k f1;
implicitIt[u1_, f1_, f2_, k_, a_, b_] := Inverse[b] . (u1 - k f2);
crankNicolsonIt[u1_, f1_, f2_, k_, a_, b_] :=
  Inverse[b + IdentityMatrix[Length[b]]] . (a . u1 + u1 - k (f1 + f2));
heatSolve[f_, g_, n_, m_, td_, it_] :=
  Module[{x, t, h, k, \[Lambda], a, b, fval, u},
   h = 1/(n + 1);
   x = Range[h, 1 - h, h];
   k = td/m;
   t = Range[0, td, k];
   \[Lambda] = k/h^2;
   a = makeAMatrix[n, \[Lambda]];
   b = makeBMatrix[n, \[Lambda]];
   fval = Outer[f, x, t];
   u = ConstantArray[0, {n, m + 1}];
   u[[All, 1]] = g /@ x;
   Do[u[[All, j]] =
     it[u[[All, j - 1]], fval[[All, j - 1]], fval[[All, j]], k, a,
      b], {j, 2, m + 1}];
   Return[
    Interpolation[
     Join[Flatten[MapThread[List, {Outer[List, x, t], u}, 2], 1],
      Map[{{0, #}, 0} &, t], Map[{{1, #}, 0} &, t]]]
    ];
   ];
```

Check the code with the following test case

```mathematica
f = Function[{x, t}, 0];
g = Function[{x}, N[Sin[2 \[Pi] x]]];
n = 20;
m = 5;
td = 0.1;
sol = heatSolve[f, g, n, m, td, explicitIt];
Plot3D[sol[x, t], {x, 0.`, 1.`}, {t, 0.`, 0.1`}, PlotRange -> Full]
```

![](https://img.duanyll.com/img/20240421170605.png)

Find the analytical solution

```mathematica
exactsol =
 DSolveValue[{D[u[x, t], t] == D[u[x, t], {x, 2}],
   u[x, 0] == Sin[2 \[Pi] x]}, u[x, t], {x, t}]
Plot3D[exactsol, {x, 0.`, 1.`}, {t, 0.`, 0.1`}, PlotRange -> Full]
```

$$
e^{-4 \pi ^2 t} \sin (2 \pi  x)
$$

![](https://img.duanyll.com/img/20240421170528.png)

Then define functions to make error plot

```mathematica
makeFigure315[f_, g_, n_, ms_, td_, tp_, it_, exact_] := Module[{sols},
   sols = Map[heatSolve[f, g, n, #, td, it] &, ms];
   Return[GraphicsColumn[Map[Function[{ts}, Show[
        ListLinePlot[
         Map[Function[{sol}, sol[#, ts] & /@ Subdivide[n + 1]], sols],
          PlotMarkers -> Automatic, DataRange -> {0, 1},
         PlotLegends -> (StringForm["M = ``", #] &) /@ ms],
        Plot[exact /. t -> ts, {x, 0, 1}, PlotStyle -> Gray,
         PlotLegends -> {"Analytical"}],
        PlotRange -> Full, AspectRatio -> 1/2, ImageSize -> Medium,
        PlotLabel -> StringForm["Time = ``", ts],
        AxesLabel -> {"x-axis", "solution"}
        ]], tp], ImageSize -> Large]];
   ];

makeFigure316FixedN[f_, g_, ns_, mpoints_, mmax_, td_, it_, exact_] :=
   ListLogLogPlot[Function[n, Module[{ms, xs, errs},
      ms = Round[Exp[Subdivide[Log[5], Log[mmax], mpoints]]];
      xs = Subdivide[n + 1];
      errs =
       Function[m,
         Max[Abs[(heatSolve[f, g, n, m, td, it][#, td] & /@
              xs) - (exact /. {t -> td, x -> xs})]]] /@ ms;
      Return[Transpose[{ms, errs}]];
      ]] /@ ns,
   PlotMarkers -> Automatic, Joined -> True,
   PlotLegends -> (StringForm["N = ``", #] &) /@ ns,
   PlotRange -> Full, AspectRatio -> 1/2, ImageSize -> Medium,
   AxesLabel -> {"M", "Error"}
   ];

makeFigure316FixedLambda[f_, g_, \[Lambda]s_, mpoints_, mmax_, td_,
   it_, exact_] :=
  ListLogLogPlot[Function[\[Lambda], Module[{ms, ns, xs, errs},
      ms =
       Round[Exp[Subdivide[Log[16 td/\[Lambda]], Log[mmax], mpoints]]];
      ns = Round[Sqrt[(\[Lambda] ms)/td] - 1];
      xs = Subdivide[n + 1];
      errs =
       MapThread[
        Function[{n, m},
         Max[Abs[(heatSolve[f, g, n, m, td, it][#, td] & /@
              xs) - (exact /. {t -> td, x -> xs})]]], {ns, ms}];
      Return[Transpose[{ms, errs}]];
      ]] /@ \[Lambda]s,
   PlotMarkers -> Automatic, Joined -> True,
   PlotLegends -> (StringForm["\[Lambda] = ``", #] &) /@ \[Lambda]s,
   PlotRange -> Full, AspectRatio -> 1/2, ImageSize -> Medium,
   AxesLabel -> {"M", "Error"}
   ];

makeFigure316FixedKH[f_, g_, khs_, mpoints_, mmax_, td_, it_,
   exact_] := ListLogLogPlot[Function[kh, Module[{ms, ns, xs, errs},
      ms = Round[Exp[Subdivide[Log[16 td/kh], Log[mmax], mpoints]]];
      ns = Round[(kh ms)/td - 1];
      xs = Subdivide[n + 1];
      errs =
       MapThread[
        Function[{n, m},
         Max[Abs[(heatSolve[f, g, n, m, td, it][#, td] & /@
              xs) - (exact /. {t -> td, x -> xs})]]], {ns, ms}];
      Return[Transpose[{ms, errs}]];
      ]] /@ khs,
   PlotMarkers -> Automatic, Joined -> True,
   PlotLegends -> (StringForm["k/h = ``", #] &) /@ khs,
   PlotRange -> Full, AspectRatio -> 1/2, ImageSize -> Medium,
   AxesLabel -> {"M", "Error"}
   ];
```

#### Explicit Method

```mathematica
makeFigure315[f, g, 20, {5, 20}, 0.1, {0.02, 0.04, 0.1}, explicitIt, exactsol]
```

![Comparison between analytical solution and explicit method solution](https://img.duanyll.com/img/20240421213246.png)

```mathematica
makeFigure316FixedN[f, g, {20, 40}, 10, 50, 0.1, explicitIt, exactsol]
```

![Error vs number of time points (M) for explicit method, fixing N](https://img.duanyll.com/img/20240421213429.png)

```mathematica
makeFigure316FixedLambda[f, g, {0.490, 0.245,
  0.1}, 5, 5000, 0.1, explicitIt, exactsol]
```

![Error vs number of time points (M) for explicit method, fixing Lambda](https://img.duanyll.com/img/20240421213653.png)

#### Implicit Method

```mathematica
makeFigure315[f, g, 20, {5, 20}, 0.1, {0.02, 0.04, 0.1}, implicitIt, exactsol]
```

![Comparison between analytical solution and implicit method solution](https://img.duanyll.com/img/20240421213815.png)

```mathematica
makeFigure316FixedN[f, g, {20,
  40}, 20, 5000, 0.1, implicitIt, exactsol]
```

![Error vs number of time points (M) for implicit method, fixing N](https://img.duanyll.com/img/20240421213854.png)

```mathematica
makeFigure316FixedLambda[f, g, {0.490, 0.245,
  0.1}, 5, 5000, 0.1, implicitIt, exactsol]
```

![Error vs number of time points (M) for implicit method, fixing Lambda](https://img.duanyll.com/img/20240421214005.png)

#### Crank-Nicolson Method

```mathematica
makeFigure315[f, g, 20, {5, 20}, 0.1, {0.02, 0.04, 0.1}, crankNicolsonIt, exactsol]
```

![Comparison between analytical solution and Crank-Nicolson method solution](https://img.duanyll.com/img/20240421214028.png)

```mathematica
makeFigure316FixedN[f, g, {20,
  40}, 20, 500, 0.1, crankNicolsonIt, exactsol]
```

![Error vs number of time points (M) for implCrank-Nicolson method, fixing N](https://img.duanyll.com/img/20240421214059.png)

```mathematica
makeFigure316FixedKH[f, g, {0.4,
  0.04}, 5, 200, 0.1, crankNicolsonIt, exactsol]
```

![Error vs number of time points (M) for implCrank-Nicolson method, fixing k/h](https://img.duanyll.com/img/20240421214124.png)

### Question (b)

First find the analytical solution

```mathematica
exactsol =
 DSolveValue[{D[u[x, t], t] == D[u[x, t], {x, 2}],
   u[x, 0] == UnitBox[2 x - 1], u[0, t] == 0, u[1, t] == 0},
  u[x, t], {x, t}]
```

$$
\underset{K=1}{\overset{\infty }{\sum }}\frac{4 e^{-\pi ^2 t K^2} \sin \left(\frac{1}{4} \pi  K\right) \sin \left(\frac{1}{2} \pi  K\right) \sin (\pi  x K)}{\pi  K}
$$

Calculating the infinite sum is not feasible, we simply limit the sum to $K=20$.

```mathematica
exactfun =
  Function[{nx, nt},
   Apply[NSum,
    exactsol /. {t -> nt, x -> nx, K[1] -> i,
      DirectedInfinity[1] -> 20}]];
Plot3D[exactfun[x, t], {x, 0.`, 1.`}, {t, 0.`, 0.1`},
 PlotRange -> Full]
```

![](https://img.duanyll.com/img/20240421222640.png)

Loss in high frequency is observed where $t$ is small, but this should be fine.

```mathematica
makeFigure317[f_, g_, n_, ms_, td_, tp_, it_, exact_] := Module[{sols},
   sols = Map[heatSolve[f, g, n, #, td, it] &, ms];
   Return[GraphicsColumn[Map[Function[{ts}, Show[
        ListLinePlot[
         Map[Function[{sol}, sol[#, ts] & /@ Subdivide[n + 1]], sols],
          PlotMarkers -> {Automatic, Tiny}, DataRange -> {0, 1},
         PlotLegends -> (StringForm["M = ``", #] &) /@ ms],
        Plot[exact[x, ts], {x, 0, 1}, PlotStyle -> Gray,
         PlotLegends -> {"Analytical"}],
        PlotRange -> Full, AspectRatio -> 1/2, ImageSize -> Medium,
        PlotLabel -> StringForm["Time = ``", ts],
        AxesLabel -> {"x-axis", "solution"}
        ]], tp], ImageSize -> Large]];
   ];
```

#### Implicit Method

```mathematica
makeFigure317[f, g, 30, {20, 80, 160}, 0.1, {0.02, 0.04,
  0.1}, implicitIt, exactfun]
```

![](https://img.duanyll.com/img/20240421222944.png)

#### Crank-Nicolson Method

```mathematica
makeFigure317[f, g, 30, {20, 80, 160}, 0.1, {0.02, 0.04,
  0.1}, crankNicolsonIt, exactfun]
```

![](https://img.duanyll.com/img/20240421223025.png)

For Crank-Nicolson method, the amplification factor $\kappa$ is

$$
\kappa=\frac{1-q}{1+q}
$$

Though $\lim_{q\to\infty}\kappa=1$, we still have $\lim_{q\to\infty}\kappa^q$ bounded. Therefore the jumps in the solution are not amplified to infinity and finally decays when $M$ goes large.
