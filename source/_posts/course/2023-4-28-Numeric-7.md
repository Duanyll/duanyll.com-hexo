---
title: 数值分析作业 - 插值
tags:
  - 数值分析
---

## Problem 1

![](https://cdn.duanyll.com/img/20230428221906.png)

Lagrange 插值: 

$$
l_0(x)=\frac{(x-2)(x-3)(x-5)}{(-1-2)(-1-3)(-1-5)}=-\frac{x^3}{60}+\frac{x^2}{6}-\frac{31 x}{60}+\frac{1}{2}
$$

$$
l_1(x)=\frac{(x+1)(x-3)(x-5)}{(2+1)(2-3)(2-5)}=\frac{x^3}{9}-\frac{7 x^2}{9}+\frac{7 x}{9}+\frac{5}{3}
$$

$$
l_2(x)=\frac{(x+1)(x-2)(x-5)}{(3+1)(3-2)(3-5)}=-\frac{x^3}{8}+\frac{3 x^2}{4}-\frac{3 x}{8}-\frac{5}{4}
$$

$$
l_3(x)=\frac{(x+1)(x-2)(x-3)}{(5+1)(5-2)(5-3)}=\frac{x^3}{36}-\frac{x^2}{9}+\frac{x}{36}+\frac{1}{6}
$$

$$
g(x)=0\cdot l_0(x)+l_1(x)+l_2(x)+2l_3(x)=\frac{x^3}{24}-\frac{x^2}{4}+\frac{11 x}{24}+\frac{3}{4}
$$

Newton 插值:

| $x$  | $c_0$ | $c_1$         | $c_2$           | $c_3$          |
| ---- | ----- | ------------- | --------------- | -------------- |
| $-1$ | $0$   | $\frac{1}{3}$ | $-\frac{1}{12}$ | $\frac{1}{24}$ |
| $2$  | $1$   | $0$           | $\frac{1}{6}$   |                |
| $3$  | $1$   | $\frac{1}{2}$ |                 |                |
| $5$  | $2$   |               |                 |                |

$$
\begin{aligned}
  P_3(x)&=0+\frac{1}{3}(x+1)-\frac{1}{12}(x+1)(x-2)+\frac{1}{24}(x+1)(x-2)(x-3)\\
  &=\frac{x^3}{24}-\frac{x^2}{4}+\frac{11 x}{24}+\frac{3}{4}
\end{aligned}
$$

插值的结果是相同的.

## Problem 2

![](https://cdn.duanyll.com/img/20230429214611.png)

$$
\begin{cases}
  S_1(x)=a_1+b_1x+c_1x^2+d_1x^3 & x\in[-1,1)\\
  S_2(x)=a_2+b_2x+c_2x^2+d_2x^3 & x\in[1,2]
\end{cases}
$$

$$
\begin{cases}
  S_1(-1)=1 & a_1-b_1+c_1-d_1=1\\
  S_1(1)=1 & a_1+b_1+c_1+d_1=1\\
  S_2(1)=1 & a_2+b_2+c_2+d_2=1\\
  S_2(2)=4 & a_2+2b_2+4c_2+8d_2=4\\
  S'_1(1)=S'_2(1) & b_1+2c_1+3d_1-b_1-2c_1-3d_1=0\\
  S''_1(1)=S''_2(2) & 2c_1+6d_1-2c_2-6d_2=0\\
  S''_1(-1)=0 & 2c_1-6d_2=0\\
  S''_2(2)=0 & 2c_2+12d_2=0\\
\end{cases}
$$

$$
\begin{cases}
  S_1(x)=\frac{1}{4}-\frac{1}{4}x+\frac{3}{4}x^2+\frac{1}{4}x^3 & x\in[-1,1)\\
  S_2(x)=1+-\frac{5}{2}x+3x^2-\frac{1}{2}x^3 & x\in[1,2]
\end{cases}
$$

## Problem 3

![](https://cdn.duanyll.com/img/20230429215111.png)

| $x$ | $c_0$ | $c_1$             | $c_2$            | $c_3$ | $c_4$ | $c_5$ |
| --- | ----- | ----------------- | ---------------- | ----- | ----- | ----- |
| $1$ | $2$   | $\color{red}{5}$  | $\color{red}{5}$ | $-8$  | $7$   | $-2$  |
| $1$ | $2$   | $\color{red}{5}$  | $-3$             | $-1$  | $3$   |
| $1$ | $2$   | $2$               | $-4$             | $5$   |
| $2$ | $4$   | $\color{red}{-2}$ | $6$              |
| $2$ | $4$   | $4$               |
| $3$ | $8$   |

$$
\begin{aligned}
  P_5(x)&=2+5(x-1)+5(x-1)^2-8(x-1)^3+7(x-1)^3(x-2)-2(x-1)^3(x-2)^2\\
        &=-2 x^5+21 x^4-81 x^3+142 x^2-110 x+32
\end{aligned}
$$

## Problem 4

![](https://cdn.duanyll.com/img/20230429221039.png)

下面的 Mathematica 程序给定插值点和函数值, 计算 Newton 差商矩阵的第一行 $c$ 并借此求出插值函数

```mathematica
newtonCoeffcient[xi_?ListQ, fi_?ListQ] := Module[{a, n, c}, (
    n = Length[xi];
    a = fi;
    c = ConstantArray[0, n];
    c[[1]] = a[[1]];
    Do[(
      Do[
       a[[j]] = (a[[j + 1]] - a[[j]])/(xi[[i + j]] - xi[[j]]), {j, 1, 
        n - i}];
      c[[i + 1]] = a[[1]];
      ), {i, 1, n - 1}];
    Return[c];
    )];
newtonValue[xi_?ListQ, a_?ListQ, x_] := 
  Fold[#2[[2]] + (x - #2[[1]]) #1 &, 0, Reverse[Transpose[{xi, a}]]];
```

先使用 Mathematica 内置的 `InterpolatingPolynomial` 计算插值多项式

```mathematica
xi = {0, \[Pi]/6, \[Pi]/3, \[Pi]/4, \[Pi]/2};
fi = Sin[xi];
pStd = InterpolatingPolynomial[Transpose[{xi, fi}], x] // Expand;
pStd // TeXForm
```

$$
-\frac{648 \sqrt{3} x^4}{\pi ^4}+\frac{1152 \sqrt{2} x^4}{\pi
   ^4}-\frac{504 x^4}{\pi ^4}+\frac{594 \sqrt{3} x^3}{\pi
   ^3}-\frac{1152 \sqrt{2} x^3}{\pi ^3}+\frac{594 x^3}{\pi
   ^3}-\frac{162 \sqrt{3} x^2}{\pi ^2}+\frac{352 \sqrt{2} x^2}{\pi
   ^2}-\frac{217 x^2}{\pi ^2}+\frac{27 \sqrt{3} x}{2 \pi }-\frac{32
   \sqrt{2} x}{\pi }+\frac{25 x}{\pi }
$$

再使用 Newton 法计算

```mathematica
cNewton = newtonCoeffcient[xi, fi] // Simplify;
cNewton // TeXForm
```

$$
\left\{0,\frac{3}{\pi },\frac{9 \left(\sqrt{3}-2\right)}{\pi
   ^2},-\frac{36 \left(-6+8 \sqrt{2}-3 \sqrt{3}\right)}{\pi
   ^3},\frac{72 \left(-7+16 \sqrt{2}-9 \sqrt{3}\right)}{\pi
   ^4}\right\}
$$

```mathematica
pNewton = newtonValue[xi, cNewton, x] // Expand;
pNewton // TeXForm
```

$$
-\frac{648 \sqrt{3} x^4}{\pi ^4}+\frac{1152 \sqrt{2} x^4}{\pi
   ^4}-\frac{504 x^4}{\pi ^4}+\frac{594 \sqrt{3} x^3}{\pi
   ^3}-\frac{1152 \sqrt{2} x^3}{\pi ^3}+\frac{594 x^3}{\pi
   ^3}-\frac{162 \sqrt{3} x^2}{\pi ^2}+\frac{352 \sqrt{2} x^2}{\pi
   ^2}-\frac{217 x^2}{\pi ^2}+\frac{27 \sqrt{3} x}{2 \pi }-\frac{32
   \sqrt{2} x}{\pi }+\frac{25 x}{\pi }
$$

得到的结果与内置函数完全一致.

将给定点直接代入插值函数求近似值, 在 $(0,\pi/2)$ 以外的区域显然存在巨大的误差

```mathematica
x = N[{1, 2, 3, 4, 14, 1000}];
Map[newtonValue[xi, cNewton, #] &, x] // TeXForm
```

$$
\{0.841456,0.902773,-0.00539706,-1.38127,563.687,2.85928\times
   10^{10}\}
$$

利用三角函数的周期性

```mathematica
pSin[x_] := Piecewise[{
    {newtonValue[xi, cNewton, Mod[x, 2 \[Pi]]], 
     Mod[x, 2 \[Pi]] < \[Pi]/2},
    {newtonValue[xi, cNewton, \[Pi] - Mod[x, 2 \[Pi]]], 
     Mod[x, 2 \[Pi]] < \[Pi]},
    {-newtonValue[xi, cNewton, Mod[x, 2 \[Pi]] - \[Pi]], 
     Mod[x, 2 \[Pi]] < (3 \[Pi])/2},
    {-newtonValue[xi, cNewton, 2 \[Pi] - Mod[x, 2 \[Pi]]], True}
    }];
Map[pSin, x] // TeXForm
```

得到的结果是合理的

$$
\{0.841456,0.909353,0.140833,-0.756786,0.990837,0.826859\}
$$

绝对误差

```mathematica
Map[pSin, x] - Sin[x] // TeXForm
```

$$
\{-0.0000153766,0.0000553328,-0.000286649,0.0000161581,0.000229293,-0.
   0000203395\}
$$

可以注意到绝对误差均在小数点后三位.

```mathematica
Plot[pSin[x] - Sin[x], {x, 0, \[Pi]/2}]
```

![](https://cdn.duanyll.com/img/20230429233933.png)

## Problem 5

![](https://cdn.duanyll.com/img/20230429232822.png)

定义一些辅助的变量和函数

```mathematica
uniformSample[a_, b_, n_] := Range[a, b, (b - a)/(n - 1)];
chebshevSample[a_, b_, n_] := 
  Table[(b - a)/2 Cos[((2 i + 1) \[Pi])/(2 (n + 1)) + (b + a)/2], {i, 
    0, n}];
f[x_] := 1/(1 + 12 x^2);
newton[f_, xi_, x_] := 
  newtonValue[xi, newtonCoeffcient[xi, f[xi]], x] // Expand;
l = -2.;
r = 2.;
```

取 $n=11,21,32$ 进行插值并绘制图像

```mathematica
Clear[x];
GraphicsGrid[ParallelMap[{
    Plot[
     {f[x], newton[f, uniformSample[l, r, #], x], 
      newton[f, chebshevSample[l, r, #], x]},
     {x, l, r},
     PlotLegends -> Placed[{"f[x]", "uniform", "chebshev"}, Below],
     PlotLabel -> "n=" <> TextString[#]],
    Plot[
     {f[x] - newton[f, uniformSample[l, r, #], x], 
      f[x] - newton[f, chebshevSample[l, r, #], x]},
     {x, l, r},
     PlotLegends -> Placed[{"uniform", "chebshev"}, Below],
     PlotLabel -> "Error n=" <> TextString[#]]
    } &, {11, 21, 31}]]
```

![](https://cdn.duanyll.com/img/20230430105051.png)

注意到, 在中间的 $(-1,1)$ 范围内, 两种插值方法得到的函数的形态都接近原函数, 由于均匀方式选取的插值节点更加密集, 在原点附近均匀插值的绝对误差小于 Chebshev 插值. 然而在接近边界区域, 均匀插值方法表现出了明显的震荡现象, 完全不能反映原函数的走向, 而 Chebshev 插值的结果与原函数相比误差较小.

## Problem 6

![](https://cdn.duanyll.com/img/20230430105911.png)

实现三次自然样条插值

```mathematica
splineCoefficient[xi_?ListQ, fi_?ListQ] := 
  Module[{n, a, \[Delta], \[CapitalDelta], m, c, \[Beta], d, b}, (
    n = Length[xi];
    a = Drop[fi, -1];
    \[Delta] = Table[xi[[i + 1]] - xi[[i]], {i, 1, n - 1}];
    \[CapitalDelta] = Table[fi[[i + 1]] - fi[[i]], {i, 1, n - 1}];
    m = SparseArray[Flatten[{
        {1, 1} -> 1, {n, n} -> 1,
        Table[{{i, i + 1} -> \[Delta][[i]], {i, i} -> 
           2 \[Delta][[i - 1]] + 2 \[Delta][[i]], {i, 
            i - 1} -> \[Delta][[i - 1]]}, {i, 2, n - 1}]
        }], {n, n}];
    \[Beta] = 
     SparseArray[
      Table[{i} -> 
        3 (\[CapitalDelta][[i]]/\[Delta][[i]] - \[CapitalDelta][[i - 
              1]]/\[Delta][[i - 1]]), {i, 2, n - 1}], {n}];
    c = Normal[LinearSolve[m, \[Beta]]];
    d = Table[(c[[i + 1]] - c[[i]])/(
      3 \[Delta][[i]]), {i, 1, n - 1}];
    b = Table[\[CapitalDelta][[i]]/\[Delta][[i]] - \[Delta][[i]]/
        3 (2 c[[i]] + c[[i + 1]]), {i, 1, n - 1}];
    c = Drop[c, -1];
    Return[Transpose[{Drop[xi, -1], a, b, c, d}]];
    )];
splineValue[data_, x_] := Module[{a, b, c, d, xi}, (
    {xi, a, b, c, d} = 
     SelectFirst[Reverse[data], x >= #[[1]] &, First[data]];
    Return[a + (b + (c + d (x - xi)) (x - xi)) (x - xi)];
    )];
```

并绘制图形

```mathematica
GraphicsGrid[ParallelMap[Module[{xi, data},
    xi = uniformSample[l, r, #];
    data = splineCoefficient[xi, f[xi]];
    {Plot[
      {f[x], newton[f, xi, x], splineValue[data, x]},
      {x, l, r},
      PlotLegends -> Placed[{"f[x]", "uniform", "spline"}, Below],
      PlotLabel -> "n=" <> TextString[#]],
     Plot[
      {f[x] - newton[f, xi, x], f[x] - splineValue[data, x]},
      {x, l, r},
      PlotLegends -> Placed[{"uniform", "spline"}, Below],
      PlotLabel -> "Error n=" <> TextString[#]]
     }] &, {11, 21, 31}]]
```

![](https://cdn.duanyll.com/img/20230430144824.png)

可以注意到样条法不会发生端点附近剧烈震荡的情况, 误差也要小于高次插值, 但是参数量较大.