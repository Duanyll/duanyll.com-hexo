---
title: Differential Equations - Gray Scott Equations
tags:
  - 微分方程
tikzjax: true
---

## Introduction to Gray Scott Equations

The Gray-Scott equations are a set of partial differential equations used to model reaction-diffusion systems, particularly in chemistry and biology. They describe the interaction between two chemical species, typically referred to as $u$ and $v$, that diffuse and react with each other over time and space.

The Gray-Scott model is known for producing a variety of complex, spatially structured patterns, such as spots, stripes, and waves. The nature of the patterns depends on the parameters $D_u$, $D_v$, $F$, and $k$. The model can exhibit both steady-state and oscillatory behavior.

### Formulation of Gray-Scott Equations

The Gray-Scott model is defined by the following system of PDEs:

$$
\frac{\partial u}{\partial t} = D_u \nabla^2 u - uv^2 + F(1 - u) \label{eq:u}
$$

$$
\frac{\partial v}{\partial t} = D_v \nabla^2 v + uv^2 - (F + k)v \label{eq:v}
$$

where:
- $u$ and $v$ are the concentrations of the two chemical species.
- $D_u$ and $D_v$ are the diffusion coefficients of $u$ and $v$, respectively.
- $F$ is the feed rate of the reactant $u$.
- $k$ is the rate at which $v$ decays.
- $\nabla^2$ is the Laplacian operator, representing diffusion in space.

### Interpretation of Terms

- **Diffusion Terms:** $D_u \nabla^2 u$ and $D_v \nabla^2 v$ represent the spatial diffusion of species $u$ and $v$. These terms cause the chemicals to spread out over time.
- **Reaction Terms:** $-uv^2$ and $+uv^2$ represent the interaction between $u$ and $v$. The term $-uv^2$ in the equation for $u$ indicates that $u$ is consumed in the reaction to produce $v$, while the term $+uv^2$ in the equation for $v$ indicates that $v$ is produced in the reaction.
- **Feed and Decay Terms:** $F(1 - u)$ represents the addition of species $u$ into the system at a constant rate, while $(F + k)v$ represents the decay of species $v$.

## Derivation of Numerical Solutions

We try to keep everything simple. The Gray-Scott equations are discretized in space using a finite difference method, and the resulting system of ODEs is solved using explicit Euler time-stepping. 

To give the formulation, we use $t'$ to denote the time step size, and $h_x, h_y$ to denote the spatial step sizes. For the time derivative, we approximate it using the forward difference:

$$
\frac{\partial u}{\partial t} \approx \frac{u(t + t') - u(t)}{t'}
$$

The Laplacian in 2D cartesian coordinates is approximated using the five-point stencil:

$$
\nabla^2 u = \frac{\p^2 u}{\p x^2}+\frac{\p^2 u}{\p y^2} \approx \frac{u_{i+1,j} - 2u_{i,j} + u_{i-1,j}}{h_x^2} + \frac{u_{i,j+1} - 2u_{i,j} + u_{i,j-1}}{h_y^2}
$$

$v$ is updated in a similar way. New values of $u$ and $v$ are calculated using the following update rules:

$$
\begin{aligned}
    l_u&\larr \frac{u_{i+1,j} - 2u_{i,j} + u_{i-1,j}}{h_x^2} + \frac{u_{i,j+1} - 2u_{i,j} + u_{i,j-1}}{h_y^2} \\
    l_v&\larr \frac{v_{i+1,j} - 2v_{i,j} + v_{i-1,j}}{h_x^2} + \frac{v_{i,j+1} - 2v_{i,j} + v_{i,j-1}}{h_y^2} \\
    d_u&\larr D_u l_u - u_{i,j}v_{i,j}^2 + F(1 - u_{i,j}) \\
    d_v&\larr D_v l_v + u_{i,j}v_{i,j}^2 - (F + k)v_{i,j} \\
    \hat{u}_{i,j}&\larr u_{i,j} + t' d_u \\
    \hat{v}_{i,j}&\larr v_{i,j} + t' d_v
\end{aligned}
\label{eq:update}
$$

$\hat{u}, \hat{v}$ denote $u, v$ at the next time step. The update rules are applied to all grid points $(i, j)$ in the domain. After updating all grid points, we set $u = \hat{u}, v = \hat{v}$ and repeat the process for the next time step.

Here we formally present the algorithm for solving the Gray-Scott equations:

1. Use a rectangular grid to discretize the spatial-temporal domain.
   $$
   \begin{aligned}
         x_i &= i \cdot h_x, \quad i = 0, 1, \cdots, N_x + 1 \\
         y_j &= j \cdot h_y, \quad j = 0, 1, \cdots, N_y + 1 \\
         t_n &= n \cdot t', \quad n = 0, 1, \cdots, N_t + 1
   \end{aligned}
   $$
2. Use forward difference to approximate the time derivative, and five-point central difference to approximate the Laplacian to obtain $(\ref{eq:u})$ at each grid point.
   $$
   \begin{aligned}
        &\frac{U(x_i,y_j,t_{n+1})-U(x_i,y_j,t_n)}{t'}+O(t')\\
        =\;&D_u\left(\frac{U(x_{i+1},y_j,t_n)-2U(x_i,y_j,t_n)+U(x_{i-1},y_j,t_n)}{h_x^2}+O(h_x^2)\right)\\
        +\;&D_u\left(\frac{U(x_i,y_{j+1},t_n)-2U(x_i,y_j,t_n)+U(x_i,y_{j-1},t_n)}{h_y^2}+O(h_y^2)\right)\\
        -\;&U(x_i,y_j,t_n)V(x_i,y_j,t_n)^2+F(1-U(x_i,y_j,t_n))\\
   \end{aligned}
   $$
   The term $O(t'), O(h_x^2), O(h_y^2)$ form the truncation error of the finite difference method. The same applies to $(\ref{eq:v})$. The equation applies for time steps $n=1,2,\cdots,N_t$。
3. Drop the truncation error terms and rearrange the equation to obtain the update rule $(\ref{eq:update})$.
4. The initial condition (IC) is set by assigning given values to all $u, v$ at $t = 0$. The periodic boundary condition (BC) is set by copying the values of $u, v$ at the opposite boundary, i.e.,
   $$
   \begin{aligned}
   u_{i,0,n} = u_{i,N_y,n}, \quad u_{i,N_y+1,n} = u_{i,1,n}, \\
   u_{0,j,n} = u_{N_x,j,n}, \quad u_{N_x+1,j,n} = u_{1,j,n}, \\
   \end{aligned}
   $$
   and the same for $v$. The boundary condition is applied at each time step $n=1,2,\cdots N_t$.

The update rule $(\ref{eq:update})$ can be efficiently implemented on fragment shaders, which are widely supported by modern GPUs. We use a straightforward implementation in GLSL to demonstrate the algorithm.

```glsl
#version 330

in vec2 uv0;
uniform sampler2D texture0;
out vec4 fragColor;

uniform float c_du;
uniform float c_dv;
uniform float c_f;
uniform float c_k;

uniform float dx;
uniform float dy;
uniform float hx;
uniform float hy;
uniform float dt;

void main() {
    vec2 center = texture(texture0, uv0).xy;
    vec2 left = texture(texture0, uv0 + vec2(-dx, 0)).xy;
    vec2 right = texture(texture0, uv0 + vec2(dx, 0)).xy;
    vec2 bottom = texture(texture0, uv0 + vec2(0, -dy)).xy;
    vec2 top = texture(texture0, uv0 + vec2(0, dy)).xy;

    float lu = (left.x + right.x - 2.0 * center.x) / (hx * hx) 
        + (top.x + bottom.x - 2.0 * center.x) / (hy * hy);
    float lv = (left.y + right.y - 2.0 * center.y) / (hx * hx) 
        + (top.y + bottom.y - 2.0 * center.y) / (hy * hy);
    float du = c_du * lu - center.x * center.y * center.y + c_f * (1.0 - center.x);
    float dv = c_dv * lv + center.x * center.y * center.y - (c_f + c_k) * center.y;
    vec2 result = center + vec2(du, dv) * dt;

    fragColor = vec4(result, 0.0, 1.0);
}
```

The periodic boundary condition is not explicitly implemented in the shader. Instead, we use the `GL_REPEAT` texture wrapping mode to handle the boundary condition.

## Test of Accuracy and Stability

### Accuracy

It is difficult to obtain an analytical solution for the Gray-Scott equations. From the truncation error term, we could expect the method to have a first-order accuracy in time and second-order accuracy in space. 

### Convergence

We could empirically show that the method converges under the condition

$$
t'\leq \min\{\frac{h^2}{4D_u},1\}.\label{eq:convergence}
$$

First we test the convergence of the method by increasing the spatial resolution while keeping the time step size constant. We set $D_u=2\times 10^{-5}$, $D_v=10^{-5}$, $F=0.04$ and $k=0.06$. We use a fixed time step size $t'=0.9$ and change the spatial resolution. The result indicate that the system fail to start evolving when the spatial resolution is too low. However, when the spatial resolution is too high, the system is numerically unstable and diverges in a few time steps. It is only possible to obtain stable results when the spatial resolution is within a certain range. Note that since different random noise is added to the initial condition, the exact pattern of the solution may vary in each simulation.

![Simulation result at T=20000 with different spatial resolutions. The solution diverges when N > 291.](https://cdn.duanyll.com/img/20240703213301.png)

We also tried to change the time step size while keeping the spatial resolution constant. The result shows that lower time step size leads to consistent results, and the system diverges when the time step size exceeds a certain value.

![Simulation result at T=20000 and N=256 with different t'. The solution diverges when t' > 1.17](https://cdn.duanyll.com/img/20240703210645.png)

We finally demonstrate that by decreasing the time step size and increasing the spatial resolution simultaneously, the method could be conditionally convergent. Following the condition $(\ref{eq:convergence})$, we set $t'$ to $0.9\min\{{h^2}/{4D_u},1\}$ in these simulation. The result shows that the method is convergent under this condition.

![Simulation result when the convergence condition is satified.](https://cdn.duanyll.com/img/20240703212537.png)

### Stability

We test the stability of the method by extending final simulation time $T$ while keeping the spatial resolution and time step size constant. The result shows that the system is stable and the solution converges to a steady state.

![Simulation result at different time steps](https://cdn.duanyll.com/img/20240703213154.png)

## Simulation Results

We wrote a GPU-accelerated simulation program using OpenGL and GLSL. The program solves the Gray-Scott equations on a 2D grid and visualizes the results in real-time. 

![Screenshot of the simulation program](https://cdn.duanyll.com/img/20240703221930.png)

The program contains a GUI that allows users to adjust the parameters of the Gray-Scott equations, such as $D_u$, $D_v$, $F$, and $k. Users can observe the patterns generated by the model in real-time and explore the dynamics of reaction-diffusion systems. To run the program, simply run `python grayscott.py` in the terminal.

We present the results of the simulation with different parameters. The patterns generated by the Gray-Scott equations are highly sensitive to the parameters. By adjusting these parameters, we can observe a wide range of patterns, including spots, stripes, and waves. The following images show the phase diagram of the Gray-Scott equation and the results obtained at different locations in the domain. We set $D_u=2\times 10^{-5}$, $D_v=10^{-5}$, $N=256$, $t'=0.9$, and $T=20000$.

![The phase diagram of the Gray-Scott equation (Pearson 1993)](https://cdn.duanyll.com/img/20240703221402.png)

![Results obtained at different conditions](https://cdn.duanyll.com/img/20240703221338.png)

## Conclusion

The Gray-Scott equations are a powerful tool for modeling reaction-diffusion systems and generating complex spatial patterns. By discretizing the equations and solving them numerically, we can observe a wide range of patterns, including spots, stripes, and waves. The numerical method we presented is conditionally convergent and stable under certain conditions. By adjusting the parameters of the model, we can explore the rich dynamics of reaction-diffusion systems and gain insights into the underlying mechanisms that give rise to complex patterns in nature.